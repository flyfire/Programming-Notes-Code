# Gradle多项目构建

<br/>

# 1 模块化

将代码分离成模块是一个巨大的任务，两个指导原则：**最小化的耦合和最大化的内聚**。

- 耦合用来衡量特定的代码工件如类与类之间关系的强弱
- 内聚涉及了模块之间的组件之间的关系密切程度

<br/><br/><br/>
# 2 用Gradle构建多项目

示例：把一个项目分为下面三个项目：

    ├─model          //项目
    │
    ├─repository       //项目
    │
    ├─web              //项目
    │
    └─build.gradle


使用`gradle projects`可以查看参与构建的项目：

对于只有一个build.gradle构建脚本的构建来说，表示只有一个项目参与构建

    gradle projects
    Parallel execution with configuration on demand is an incubating feature.
    :projects

    ------------------------------------------------------------
    Root project
    ------------------------------------------------------------

    Root project 'todo-modularized'
    No sub-projects

    To see a list of the tasks of a project, run gradle <project-path>:tasks
    For example, try running gradle :tasks

    BUILD SUCCESSFUL


## 2.1 setting文件

setting文件声明了所需要的配置来实例化项目的层次结构，默认情况下名称为`setting.gradle`,在setting中使用include方法来将子项目添加到构建中：

    include 'model'
    include 'repository', 'web'

- `include 'model'`中路径是相对于根项目的目录
- 如果构建了深层次的项目结构，使用冒号(:)字符来分隔每一个子项目结构。比如想要映射model//todo/items目录结构，则可以使用`include 'model:todo:、items'`来添加子项目，此时model、todo、items都是参与构建的项目
- 如果只是希望把模块放到深层的目录可以通过改变模块的projectDir：`project(':SubmoduleA').projectDir = new File('Submodules/SubmoduleA')`

在此运行`gradle projects`

    ------------------------------------------------------------
    Root project
    ------------------------------------------------------------

    Root project 'listing_06_02-todo-specific-project-behavior'
    +--- Project ':model'
    +--- Project ':repository'
    \--- Project ':web'


Gradle组装构建之前，会根据setting.gradle创建一个Setting类型的实例，Setting接口是setting文件的直接表示。其作用是添加Project参与多项目构建

**多项目构建相关API**:

Settings：包含参与构建的项目

    include(path)
    project(path)
    getRootDir()

ProjectDesriptor     项目描述符

    getBuildFile()
    getName()
    getParent()
    getProjectDir()

 Gradle对象

    getRootProject()

setting文件在任何project实例配置之前(Project实例被创建但是没有被配置)的初始化阶段执行

    //在gradle API提供的settingsEvaluated回调方法中获取Settings实例
    gradle.settingsEvaluated {Settings settings->
       ......
    }

>在命令行中运行某个模块的task，比如运行子模块A的taskA运行，应该使用`gradlew :A:taskA`命令


## 2.2 分层布局与扁平布局

Gradle多项目布局分为分层布局与扁平布局，一般我们使用分层布局，像Android使用的也是分层布局。

## 2.3 配置子项目

配置子项目涉及到Project的一些新的API

        void allprojects(Closure configureClosure);//公共的项目配置
        void subprojects(Closure configureClosure);//公共的项目配置
        Project project(String path) throws UnknownProjectException;//获取特定的项目
        Project evaluationDependsOn(String path) throws UnknownProjectException;//解析项目顺序
        void evaluationDependsOnChildren();//解析项目顺序

**为了声明特定的项目构建代码，使用project方法，至少必须提供项目路劲(如,`:model`)**

allprojects用于配置所有的项目，subprojects用于配置所有的子项目，多项目默认按照项目字母顺序进行构建，而不是setting中声明的顺序。

**直接在根项目配置子项目**

    ext.projectIds = ['group': 'com.manning.gia', 'version': '0.1']
    group = projectIds.group
    version = projectIds.version
    project(':model') {//单独为model项目进行构配置
        group = projectIds.group
        version = projectIds.version
        apply plugin: 'java'
    }
    project(':repository') {//单独为repository项目进行构配置
        group = projectIds.group
        version = projectIds.version
        apply plugin: 'java'
    }
    project(':web') {//单独为web项目进行构配置
        group = projectIds.group
        version = projectIds.version
        apply plugin: 'java'
        apply plugin: 'war'
        apply plugin: 'jetty'
        repositories {
            mavenCentral()
        }
        dependencies {
            providedCompile 'javax.servlet:servlet-api:2.5'
            runtime 'javax.servlet:jstl:1.1.2'
        }
    }

有一点需要注意：**在一个项目中定义的属性会自动被其子项目继承。**

**声明项目依赖**：如一个项目依赖与另一个项目，可以使用下面代码声明项目依赖

    project(':model') {
         ......
    }

    project(':repository') {
        ......
        dependencies {
            compile project(':model')
        }
    }

    project(':web') {
        ......
        repositories {
            mavenCentral()
        }
        dependencies {
            compile project(':repository')
            providedCompile 'javax.servlet:servlet-api:2.5'
            runtime 'javax.servlet:jstl:1.1.2'
        }
    }

## 2.4 多项目部分构建

Gradle支持多项目的部分构建，这对与提高构建效率很有作用：

- `-a` repository依赖module，假设子项目repository只改变了代码，但不想重新构建子项目model：`gradle :repository:build -a`
- 在一个项目中如果只改变了文件，`--no-rebuild`选项可很好的发挥作用
- 项目的任何改变都可能对依赖它的其他项目产生副作用，在buildDepeneents task作用下，通过构建和测试依赖的项目来验证代码变化产生的影响：`gradle:repository:在buildDepeneents`


## 2.5 声明跨项目的task依赖

    ext.projectIds = ['group': 'com.manning.gia', 'version': '0.1']

    group = projectIds.group
    version = projectIds.version

    task hello << {
        println 'Hello from root project'
    }

    project(':model') {
        group = projectIds.group
        version = projectIds.version
        apply plugin: 'java'

        task hello(dependsOn: ':repository:hello') << {
            println 'Hello from model project'
        }
    }

    project(':repository') {
        group = projectIds.group
        version = projectIds.version
        apply plugin: 'java'

        dependencies {
            compile project(':model')
        }

        task hello << {
            println 'Hello from repository project'
        }
    }

    project(':web') {
        group = projectIds.group
        version = projectIds.version
        apply plugin: 'java'
        apply plugin: 'war'
        apply plugin: 'jetty'

        repositories {
            mavenCentral()
        }

        dependencies {
            compile project(':repository')
            providedCompile 'javax.servlet:servlet-api:2.5'
            runtime 'javax.servlet:jstl:1.1.2'
        }
    }


## 2.6 定义公共行为

    allprojects {//为根项目和所有的子项目配置
        group = 'com.manning.gia'
        version = '0.1'
    }

    subprojects {//为所有的子项目配置
        apply plugin: 'java'
    }

## 2.7 独立的项目文件

如果参与构建的项目很多，把所有子项目的配置都写在根项目的构建文件中很显然不合适，这时可以把各个项目的配置脚本单独写在各自项目的构建文件中：

    RootProject
    │  build.gradle    //根项目的构建脚本文件
    │  settings.gradle
    ├─model
    │  │  build.gradle//子项目的构建脚本文件
    ├─repository
    │  │  build.gradle
    ├─web
    │   │  build.gradle


## 2.8 自定义项目

改变项目的名称：

setting.gradle:

    include 'todo-model', 'todo-repository', 'todo-web'

    rootProject.name = 'todo'

    rootProject.children.each {
        it.buildFileName = it.name + '.gradle' - 'todo-'
    }


此时子项目的构建脚本名为：

    RootProject
    │  build.gradle
    │  settings.gradle
    ├─todo-model
    │  │  model.gradle
    ├─todo-repository
    │  │  repository.gradle
    ├─todo-web
    │    │  web.gradle
