if (isMultiApp.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    defaultConfig {

        if (isMultiApp.toBoolean()) {
            applicationId "com.ztiany.module_b"
        }
        minSdkVersion 17
        //noinspection OldTargetApi
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    dataBinding.enabled = true
    resourcePrefix "module_b"

    sourceSets {
        main {
            if (isMultiApp.toBoolean()) {
                manifest.srcFile 'src/app/AndroidManifest.xml'
                java.srcDirs += 'src/app/java/'
                res.srcDirs +='src/app/res'
            } else {
                manifest.srcFile 'src/library/AndroidManifest.xml'
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':base')
    testCompile 'junit:junit:4.12'
    annotationProcessor rootProject.daggerAnntation
    annotationProcessor rootProject.daggerAndroidAnntation
}
